apply plugin: "application"

mainClassName = 'nl.sikken.bertrik.TtnHabBridge'

dependencies {
    implementation project(':cayenne')

    implementation libraries.slf4jlog4j
    
    implementation libraries.mqtt_client
    implementation libraries.jackson
    implementation libraries.xml_bind
    implementation libraries.retrofit
    compileOnly libraries.spotbugs_annotations
    
    testImplementation libraries.retrofit_mock
}

//Add configuration folder to classpath:
startScripts {
    classpath += files('../cfg')
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile    = file getUnixScript()
             
        windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\cfg', '%APP_HOME%\\cfg')
        unixScriptFile.text    = unixScriptFile.text.replace('$APP_HOME/lib/cfg', '$APP_HOME/cfg')
    }
}

// fix scripts executable bit and line endings
import org.apache.tools.ant.filters.FixCrLfFilter

distributions {
    main {
        contents {
            filesMatching("**/*.sh") {
                mode = 0755
                filter(FixCrLfFilter,
                    eol: FixCrLfFilter.CrLf.newInstance('lf'),
                    tab: FixCrLfFilter.AddAsisRemove.newInstance('asis'),
                    eof: FixCrLfFilter.AddAsisRemove.newInstance('remove'),
                    fixlast: true)
            }
        } 
    }
}
